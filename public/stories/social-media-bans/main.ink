EXTERNAL navigateTo(story_id)

VAR topic_title = "Social Media Bans"

// For cross-story navigation
VAR came_from_age_verification = false
VAR came_from_social_media = false

INCLUDE circumvention.ink
INCLUDE socialization.ink
INCLUDE marginalized.ink
INCLUDE false_security.ink
INCLUDE solutions.ink
INCLUDE holistic.ink
INCLUDE conclusion.ink

Lets talk about...
We should ban social media for under 16s. # diagram: happy_social_media.jpg

Social media harms young people. Rising anxiety, depression, addictive algorithms. Cyberbullying, predators, explicit content.

Countries like Australia are implementing these bans. If kids can't vote or drive, why unrestricted social media access?

*   [That sounds reasonable.]
    The intent is right.
    But age verification has a fundamental problem: VPNs.
    -> The_VPN_Problem
*   [It doesn't matter if they can get around it with VPNs.]
    Right. That's exactly the issue.
    -> The_VPN_Problem

=== The_VPN_Problem ===
Anyone can use a VPN to bypass the ban. # diagram: vpn_bypass.jpg

UK bans social media for under 16s, but US doesn't? Use a VPN. Suddenly you're in New York, not London. Age gate vanishes.

Free VPNs—often unsafe, logging data, injecting ads—downloaded by teens just to stay connected. In trying to protect children, we push them toward tools that harm their privacy and security.

*   [So we ban VPNs then.]
    That's worth exploring—but it's a whole separate topic.
    -> VPN_Ban_Question
*   [And we can't ban those either?]
    Not without breaking the internet. But let's assume kids bypass the ban—what happens then?
    -> Circumvention_Reality
*   [What's the bigger picture here?]
    Right. There's something deeper happening.
    -> Social_Crisis

=== VPN_Ban_Question ===
Banning VPNs seems like the obvious solution. But it opens up a whole other can of worms about surveillance, legitimate uses, and technical feasibility.

*   [Take me through why VPN bans don't work.]
    ~ navigateTo("vpn-bans")
    -> END
*   [I'll take your word for it. What happens when kids bypass the ban?]
    -> Circumvention_Reality

